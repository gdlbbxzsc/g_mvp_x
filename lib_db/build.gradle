apply plugin: 'com.android.library'

android {
    compileSdkVersion build_versions.compileSdkVersion

    defaultConfig {
        minSdkVersion build_versions.minSdkVersion
        targetSdkVersion build_versions.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'

        buildConfigField("boolean", "LOG", log_mode.debug)

        manifestPlaceholders = manifest_placeholders

        resConfigs res_configs.configs

        common_config.each { key, value ->
            if (value instanceof Boolean) {
                buildConfigField("boolean", (key), value.toString())
            } else if (value instanceof GString || value instanceof String) {
                buildConfigField("String", (key), "\"" + value + "\"")
            } else if (value instanceof Number) {
                buildConfigField("int", (key), value.toString())
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [APPLOGIC: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "LOG", log_mode.release)
        }
    }

    flavorDimensions flavorDimensions
    productFlavors lib_productFlavors

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation project(path: ':lib_support')

    implementation project(":applogic_annotation")
    implementation project(":applogic_api")
    annotationProcessor project(":applogic_compiler")

    //   数据库
    api "androidx.room:room-runtime:$dependencies_version.room_version"
    annotationProcessor "androidx.room:room-compiler:$dependencies_version.room_version"

    //   浏览器访问 http://localhost:8090/index.html 不好使执行命令监听端口 adb forward tcp:8090 tcp:8090
    debugImplementation "io.objectbox:objectbox-android-objectbrowser:$dependencies_version.objectbox_version"
    releaseImplementation  "io.objectbox:objectbox-android:$dependencies_version.objectbox_version"

}
apply plugin: 'io.objectbox'